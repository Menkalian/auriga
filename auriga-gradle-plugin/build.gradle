plugins {
    id "java-gradle-plugin"
    id 'maven-publish'
    id 'com.gradle.plugin-publish' version '0.12.0'
    id "org.jetbrains.kotlin.jvm" version "1.4.21"
    id "org.jetbrains.kotlin.kapt" version "1.4.21"
}

group = 'de.menkalian.auriga'
version = '1.0.0'
if (System.getenv("CI_COMMIT_BRANCH") != "main" && System.getenv("CI_COMMIT_BRANCH") != null) {
    version = "${version}-${System.getenv("CI_COMMIT_BRANCH")}-SNAPSHOT"
}

java {
    withJavadocJar()
    withSourcesJar()
}

pluginBundle {
    website = "https://gitlab.com/kiliankra/auriga"
    vcsUrl = "https://gitlab.com/kiliankra/auriga.git"
    tags = ["auriga", "logging", "debug", "tool", "kotlin", "java"]
}
gradlePlugin {
    plugins {
        aurigaPlugin {
            id = "de.menkalian.auriga.plugin"
            displayName = "Auriga Plugin"
            description = "A plugin that applies the Auriga tools to your projects."
            implementationClass = "de.menkalian.auriga.AurigaGradlePlugin"
        }
    }
}

repositories {
    mavenCentral()
    jcenter()
    mavenLocal()
    maven {
        name = 'artifactory-menkalian'
        url = "http://server.menkalian.de:8081/artifactory/auriga"
    }
}

publishing {
    repositories {
        maven {
            url = uri("http://server.menkalian.de:8081/artifactory/auriga")
            name = "artifactory-menkalian"
            credentials {
                username System.getenv("MAVEN_REPO_USER")
                password System.getenv("MAVEN_REPO_PASS")
            }
        }
    }
}


dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib:1.4.21'
    implementation 'org.jetbrains.kotlin:kotlin-gradle-plugin-api:1.4.21'
    implementation "de.menkalian.auriga:auriga-kotlin-plugin:$version"

    compileOnly 'com.google.auto.service:auto-service:1.0-rc7'
    kapt 'com.google.auto.service:auto-service:1.0-rc7'
}
